task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    PUBLISH_GROUP_ID = 'io.github.gmerinojimenez'
    PUBLISH_ARTIFACT_ID = 'tweaks'
    PUBLISH_VERSION = version

    PUBLISH_RELEASE_NAME = 'Tweaks for Android'
    PUBLISH_DESCRIPTION = 'A customizable debug screen to view and edit flags that can be used for development in Jetpack Compose applications'
    PUBLISH_REPO_URL = 'https://github.com/gmerinojimenez/tweaks'
}

ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value -> ext[name] = value }
} else {
    println "File Not found"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            artifact("$buildDir/outputs/aar/${project.getName()}-enabled-release.aar")
            artifact androidSourcesJar

            pom {
                name = PUBLISH_RELEASE_NAME
                description = PUBLISH_DESCRIPTION
                url = PUBLISH_REPO_URL
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'gmerinojimenez'
                        name = 'Guillermo Merino Jiménez'
                        email = 'gmerinojimenez@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gmerinojimenez/tweaks.git'
                    developerConnection = 'scm:git:ssh://https://github.com/gmerinojimenez/tweaks.git'
                    url = 'https://github.com/gmerinojimenez/tweaks/tree/main'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.getByName("implementation").allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
        noop(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId "$PUBLISH_ARTIFACT_ID-no-op"
            version PUBLISH_VERSION

            artifact("$buildDir/outputs/aar/${project.getName()}-noop-release.aar")
            artifact androidSourcesJar

            pom {
                name = "$PUBLISH_RELEASE_NAME-no-op"
                description = PUBLISH_DESCRIPTION
                url = PUBLISH_REPO_URL
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'gmerinojimenez'
                        name = 'Guillermo Merino Jiménez'
                        email = 'gmerinojimenez@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/gmerinojimenez/tweaks.git'
                    developerConnection = 'scm:git:ssh://https://github.com/gmerinojimenez/tweaks.git'
                    url = 'https://github.com/gmerinojimenez/tweaks/tree/main'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.getByName("implementation").allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }

                    //Add a dependency with the library
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', PUBLISH_GROUP_ID)
                    dependencyNode.appendNode('artifactId', PUBLISH_ARTIFACT_ID)
                    dependencyNode.appendNode('version', PUBLISH_VERSION)
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"

            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

//signing {
//    def signingKeyId = findProperty("signingKeyId")
//    def signingKey = findProperty("signingKey")
//    def signingPassword = findProperty("signingPassword")
//    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
//    sign publishing.publications
//}

signing {
    sign publishing.publications
}
